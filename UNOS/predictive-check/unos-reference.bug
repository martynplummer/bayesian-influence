## Reference model for the UNOS data with weakly informative priors
model {

    for (i in 1:50) {
        yrep[i] ~ dbin(pi[i], n[i])
        y[i] ~ dbin(pi[i], n[i])
        logit(pi[i]) <- mu.alpha + alpha[centre[i]] +
                        (mu.beta + beta[centre[i]]) * (age[i] - 4.5)
        logpi[i] <- log(pi[i])
        log1mpi[i] <- log(1 - pi[i])
    }
    for (c in 1:10) {
        ## Sum to zero constraint on random effects
        alpha[c] <- alpha0[c] - mean(alpha0)
        beta[c] <-beta0[c] - mean(beta0)
        ## Latent random effects only affect linear predictor through
        ## their contrasts
        alpha0[c] ~ dnorm(0, tau.alpha)
        beta0[c] ~ dnorm(0, tau.beta)
    }

    ## Diffuse normal prior on main effects
    mu.alpha ~ dnorm(0.0, 1.0E-3)
    mu.beta ~ dnorm(0.0, 1.0E-3)
    
    tau.alpha ~ dscaled.gamma(1, 2)
    tau.beta ~ dscaled.gamma(1, 2)
    
    sigma.alpha <- 1/sqrt(tau.alpha)
    sigma.beta <- 1/sqrt(tau.beta)

}
