# Model with informative priors depending on hyper-parameters a (location) and b(standard deviation)
# which are passed to the model with the data.

model {
      
    for (i in 1:length(y)) {
        y[i] ~ dbern(pi[i])
        logit(pi[i]) <- mu.alpha + alpha[centre[i]] +
                        (mu.beta + beta[centre[i]]) * (age[i] - mean(age))
    }
    for (c in 1:10) {
        ## Sum to zero constraint on random effects
        alpha[c] <- alpha0[c] - mean(alpha0)
        beta[c] <-beta0[c] - mean(beta0)
        ## Latent random effects only affect linear predictor through
        ## their contrasts
        alpha0[c] ~ dnorm(0, tau.alpha)
        beta0[c] ~ dnorm(0, tau.beta)
    }

    ## Informative prior with hyper-parameters a and b for prior mean
    ## and standard deviation of intercept mu.alpha
    mu.alpha ~ dnorm(a, 1/(b^2))
    mu.beta ~ dnorm(0.17, 1/(0.05^2))

    ## Weakly informative prior on variance components
    tau.alpha ~ dscaled.gamma(1, 2)
    tau.beta ~ dscaled.gamma(1, 2)   
    
    sigma.alpha <- 1/sqrt(tau.alpha)
    sigma.beta <- 1/sqrt(tau.beta)

}
